/*
 * Copyright (c) 2018.
 *
 * This file is part of MoneyWallet.
 *
 * MoneyWallet is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published by
 * the Free Software Foundation, either version 3 of the License, or
 * (at your option) any later version.
 *
 * MoneyWallet is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU General Public License for more details.
 *
 * You should have received a copy of the GNU General Public License
 * along with MoneyWallet.  If not, see <http://www.gnu.org/licenses/>.
 */

apply plugin: 'com.android.application'

android {
    compileSdkVersion versions.compileSdkVersion
    defaultConfig {
        applicationId "com.oriondev.moneywallet"
        minSdkVersion versions.minSdkVersion
        targetSdkVersion versions.targetSdkVersion
        versionCode versions.versionCode
        versionName versions.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
        manifestPlaceholders = [
                DropBoxApiKey: ApiKey_Dropbox,
                GoogleApiKey: ApiKey_Google
        ]
        buildConfigField ('String', 'API_KEY_DROPBOX', ApiKey_Dropbox)
        buildConfigField ('String', 'API_KEY_OPEN_EXCHANGE_RATES', ApiKey_OpenExchangeRates)
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "version"
    productFlavors {
        gplay {
            dimension "version"
            versionNameSuffix "-gplay"
        }
        floss {
            dimension "version"
            versionNameSuffix "-floss"
        }
    }

}

dependencies {
    // common dependencies
    implementation supportDependencies.appcompat
    implementation supportDependencies.constraintlayout
    implementation supportDependencies.recyclerview
    implementation supportDependencies.cardview
    implementation supportDependencies.annotations
    implementation supportDependencies.design
    implementation supportDependencies.preference
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.afollestad.material-dialogs:commons:0.9.6.0'
    implementation("com.mikepenz:materialdrawer:6.0.8@aar") {
        transitive = true
    }
    implementation 'com.pnikosis:materialish-progress:1.7'
    implementation 'com.github.rubensousa:bottomsheetbuilder:1.6.1'
    implementation('com.philliphsu:bottomsheetpickers:2.4.1') {
        exclude group: 'com.android.support', module: 'appcompat-v7'
        exclude group: 'com.android.support', module: 'design'
    }
    implementation 'com.github.florent37:expansionpanel:1.1.1'
    implementation group: 'commons-io', name: 'commons-io', version: '2.6'
    implementation 'com.android.support:multidex:1.0.3'
    implementation 'net.lingala.zip4j:zip4j:1.3.2'
    implementation 'com.github.daniel-stoneuk:material-about-library:2.2.4'
    implementation 'com.github.apl-devs:appintro:v4.2.3'
    implementation 'com.andrognito.pinlockview:pinlockview:2.1.0'
    implementation 'com.andrognito.patternlockview:patternlockview:1.0.0'
    implementation 'com.multidots:fingerprint-auth:1.0.1'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.3'
    implementation 'com.github.ronaldsmartin:Material-ViewPagerIndicator:1.0.4'
    implementation 'com.jaredrummler:material-spinner:1.2.5'
    implementation 'org.dmfs:lib-recur:0.11.1'
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'

    // google play dependencies
    gplayImplementation('com.dropbox.core:dropbox-core-sdk:3.0.8')
    gplayImplementation googlePlayServices.auth
    gplayImplementation googlePlayServices.signin
    gplayImplementation googlePlayServices.places
    gplayImplementation googlePlayServices.drive

    // floss dependencies
    flossImplementation 'org.osmdroid:osmdroid-android:6.0.2'

    // test dependencies
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
}